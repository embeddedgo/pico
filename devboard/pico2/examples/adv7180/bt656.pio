// Copyright 2025 The Embedded Go Authors. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

// Program bt656data transfers the active lines of BT.656 video to the input
// FIFO. It expects a parameter and a constant in the OSR and Y registers. OSR
// specifies the number of bytes in the horizontal line. Y should contain the
// number 7 which is the 4 least significant bits of the SAV XY byte for the
// field 1. It is used to detect the beggining of the field 1 lines. The same
// bits for the field 0 are zero so there is no need for second constant.
.program bt656data

waitForPixels:
	.define advClk 14

	mov isr, null

	wait 0 pin advClk
	wait 1 pin advClk
	in pins, 8
	mov x, isr
	jmp x--, waitForPixels

	wait 0 pin advClk
	wait 1 pin advClk
	in pins, 8
	mov x, isr
	jmp x--, waitForPixels

	wait 0 pin advClk
	wait 1 pin advClk
	in pins, 4
	mov x, isr
	jmp !x, readPixels
	jmp x!=y, waitForPixels

readPixels:
	irq 0
	push
	mov x, osr // number of bytes in the horizontal line - 1 to x
readByte:
	wait 0 pin advClk
	wait 1 pin advClk
	in pins, 8
	push iffull
	jmp x--, readByte


// Program bt656ctrl sends to the control DMA the address of the next line
// buffer in RAM. It expects that the X, Y, OSR registers contain the adresses
// of three line buffers.
.program bt656ctrl
	.in 32 auto

	wait 1 irq 0
	in x, 32
	wait 1 irq 0
	in y, 32
	wait 1 irq 0
	in osr, 32
